cmake_minimum_required(VERSION 3.21 FATAL_ERROR)
project(
  hmmer_reader
  VERSION 0.5.0
  LANGUAGES C)

set(HMR_MAIN_PROJECT OFF)
if(CMAKE_SOURCE_DIR STREQUAL PROJECT_SOURCE_DIR)
  set(HMR_MAIN_PROJECT ON)
endif()

if(HMR_MAIN_PROJECT)
  set(HMR_BUILD_TESTS_DEFAULT ON)
else()
  set(HMR_BUILD_TESTS_DEFAULT OFF)
endif()

option(HMR_BUILD_TESTS "Build the unit tests" ${HMR_BUILD_TESTS_DEFAULT})
message(STATUS "HMR_MAIN_PROJECT: " ${HMR_MAIN_PROJECT})
message(STATUS "HMR_BUILD_TESTS: " ${HMR_BUILD_TESTS})

include(cmake/sanitizers.cmake)
include(cmake/warnings.cmake)

add_library(
  hmr
  src/aux.c
  src/error.c
  src/fsm.c
  src/hmr.c
  src/node.c
  src/prof.c
  src/to.c
  src/tok.c)
add_library(HMR::hmr ALIAS hmr)

set(CMAKE_EXPORT_COMPILE_COMMANDS ON)
set(CMAKE_C_STANDARD 11)
set(CMAKE_C_EXTENSIONS OFF)

target_include_directories(
  hmr
  PUBLIC $<INSTALL_INTERFACE:include>
         $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
         $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}>
  PRIVATE $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/src>)

set_target_properties(hmr PROPERTIES VERSION ${PROJECT_VERSION})
set_target_properties(hmr PROPERTIES SOVERSION ${PROJECT_VERSION_MAJOR})
target_compile_options(hmr PRIVATE ${WARNING_FLAGS})
target_compile_features(hmr PRIVATE c_std_23)

set(EXPORT_FILE ${CMAKE_CURRENT_BINARY_DIR}/hmr/export.h)
include(GenerateExportHeader)
generate_export_header(
  hmr
  BASE_NAME
  HMR
  INCLUDE_GUARD_NAME
  HMR_EXPORT_H
  EXPORT_MACRO_NAME
  HMR_API
  EXPORT_FILE_NAME
  ${EXPORT_FILE})

if(CMAKE_BUILD_TYPE STREQUAL "Release")
  include(CheckIPOSupported)
  check_ipo_supported()
  set_target_properties(hmr PROPERTIES INTERPROCEDURAL_OPTIMIZATION TRUE)
endif()

install(TARGETS hmr EXPORT hmr-targets)

install(DIRECTORY include/hmr/ DESTINATION include/hmr)

install(
  EXPORT hmr-targets
  FILE hmr-targets.cmake
  NAMESPACE HMR::
  DESTINATION lib/cmake/hmr)

include(CMakePackageConfigHelpers)

set(CMAKE_CONFIG_FILE ${CMAKE_CURRENT_BINARY_DIR}/hmr-config.cmake)
configure_package_config_file(hmr-config.cmake.in ${CMAKE_CONFIG_FILE}
                              INSTALL_DESTINATION lib/cmake/hmr)

set(CMAKE_VERSION_FILE ${CMAKE_CURRENT_BINARY_DIR}/hmr-config-version.cmake)
write_basic_package_version_file(${CMAKE_VERSION_FILE}
                                 COMPATIBILITY SameMajorVersion)

install(FILES ${CMAKE_VERSION_FILE} ${CMAKE_CONFIG_FILE}
        DESTINATION lib/cmake/hmr)
install(FILES ${EXPORT_FILE} DESTINATION include/hmr)

install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/README.md
              ${CMAKE_CURRENT_SOURCE_DIR}/LICENSE.md DESTINATION share/docs/hmr)

if(HMR_BUILD_TESTS)
  enable_testing()
  add_subdirectory(test)
endif()
